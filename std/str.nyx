
cstr :: u8*(path u8[], out u8[]) {
	if path.count >= out.count {
		return null;
	}

	for i: ..path.count {
		out[i] = path[i];
	}
	out[path.count] = 0;
	return out.data;
};

cstrlen :: usz(str u8*) {
	it := str;
	while *it {
		++it;
	}
	return usz:(it - str);
};


stou :: u8(str u8[], out u64*) {
	it := str.data;
	end := it + str.count;

	if it == end {
		return 1;
	}

	v : i64 = 0;
	while it < end {
		c := *it++;

		if c > '9' || c < '0' {
			return 1;
		}

		v *= 10;
		v += c - '0';
	}

	*out = v;
	return 0;
};

stoi :: u8(str u8[], out i64*) {
	it := str.data;
	end := it + str.count;

	if it == end {
		return 1;
	}

	sign : u8 = 0;
	if *it == '-' {
		sign = 1;
		if it++ == end {
			return 1;
		}
	}

	v : i64 = 0;
	while it < end {
		c := *it++;
		if c > '9' || c < '0' {
			return 1;
		}

		v *= 10;
		v += c - '0';
	}

	if sign {
		v = -v;
	}

	*out = v;
	return 0;
};

streq :: u8(s1 u8[], s2 u8[]) {
	if s1.count != s2.count {
		return 0;
	}

	for i: ..s1.count {
		if s1[i] != s2[i] {
			return 0;
		}
	}
	return 1;
};

has_prefix :: u8(str u8[], substr u8[]) {
	if str.count < substr.count {
		return 0;
	}
	return streq(str[0..substr.count], substr);
};

is_space :: u8(c u32) {
	return c == ' ' || c == '\r' || c == '\n' || c == '\t';
};

trim_left :: u8[](str u8[]) {
	for i: ..str.count {
		if !is_space(str[i]) {
			return str[i..str.count];
		}
	}
	return "";
};

split :: u8[](str u8[], delim u32) {
	for i: ..str.count {
		if str[i] == delim { // this check should properly read utf-8 characters
			return str[0..i];
		}
	}
	return str;
};
