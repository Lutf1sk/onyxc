import "std/unix.nyx", "std/str.nyx";

let filename_from_path :: u8[](u8[] path) {
	if !path.count {
		return null[0..0];
	}

	usz i = path.count - 1;
	while i && path[i] != '/' {
		i--;
	}
	if i {
		++i;
	}
	return (path.data + i)[0..path.count - i];
}

let file_openw :: u8(isz* fdp, u8[] path) {
	u8[PATH_MAX] path_nt;
	cstr(path, path_nt);
	*fdp = open(path_nt.data, O_WRONLY|O_TRUNC|O_CREAT, S_IRUSR|S_IWUSR);
	return *fdp < 0;
}

let file_openr :: u8(isz* fdp, u8[] path) {
	u8[PATH_MAX] path_nt;
	cstr(path, path_nt);
	*fdp = open(path_nt.data, O_RDONLY, 0);
	return *fdp < 0;
}

let file_close :: void(isz fd) {
	close(fd);
}

let file_write :: void(isz fd, u8[] data) {
	write(fd, data);
}

let file_read :: usz(isz fd, u8[] data) {
	return read(fd, data);
}

let print :: isz(u8[] str) {
	return write(1, str);
}

let print_i64 :: usz(i64 n) {
	if (n < 0) {
		print("-");
		n = -n;
	}

	u8[32] str;
	u8* end = str.data + str.count, it = end - 1;

	while n >= 10 {
		*it-- = n % 10 + '0';
		n /= 10;
	}
	*it = n + '0';

	u8[] view = it[0..usz:(end - it)];
	print(view);
	return view.count;
}

let print_h64 :: usz(u64 n) {
	u8[16] hex_conv_tab :: "0123456789ABCDEF";

	u8[16] str;
	u8* end = str.data + str.count, it = end - 1;

	while n > 0x0F {
		*it-- = hex_conv_tab[n & 0x0F];
		n >>= 4u;
	}
	*it = hex_conv_tab[n];

	u8[] view = it[0..usz:(end - it)];
	print(view);
	return view.count;
}

