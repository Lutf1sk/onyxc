import "std/bitmath.nyx", "std/mem.nyx";

HASHTAB_COUNT		: usz : 256;
HASHTAB_MASK		: usz : 0xFF;
HASHTAB_ALLOC_COUNT	: usz : 32;

HashTable :: struct {
	alc		: Allocator*;
	counts	: usz[HASHTAB_COUNT];
	vals	: void**[HASHTAB_COUNT];
};

hash :: u32(void[] mem) {
	p : u8* = mem.data;
	hash : u32 = mem.count;

	for usz i..mem.count {
		hash ^= rotl32(hash, 7) ^ p[i];
	}

	return hash;
};

htinsert :: u8(HashTable* ht, u32 hash, void* val) {
	hash &= HASHTAB_MASK;

	pcount := &ht.counts[hash];
	count := *pcount;

	if !count {
		ht.vals[hash] = val;
		++*pcount;
		return 0;
	}
	elif count == 1u {
		vals : void** = ht.alc.alloc(ht.alc.usr, HASHTAB_ALLOC_COUNT * sizeof(void*));
		if !vals { return 1; }
		vals[0] = ht.vals[hash];
		ht.vals[hash] = vals;
	}
	elif count >= HASHTAB_ALLOC_COUNT {
		return 1;
	}

	ht.vals[hash][*pcount++] = val;
	return 0;
};

htfind :: void*(HashTable* ht, u32 hash, u8(void*, void*) cmp, void* usr) {
	hash &= HASHTAB_MASK;
	count := ht.counts[hash];

	if !count {
		return null;
	}
	elif count == 1u {
		if cmp(ht.vals[hash], usr) { return ht.vals[hash]; }
		else { return null; }
	}
	elif count > HASHTAB_ALLOC_COUNT {
		return null;
	}

	for usz i..count {
		if cmp(ht.vals[hash][i], usr) {
			return ht.vals[hash][i];
		}
	}
	return null;
};

